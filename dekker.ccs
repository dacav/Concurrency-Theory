* -- Abstract boolean variable specification ------------------------------

agent Bool (true, false, get, set, unset)
    = 'set.Bool(false, true, get, unset, set)
    + 'get.false.Bool(true, false, get, set, unset)
    ;

* -- Abstract protocol specification --------------------------------------

agent Proc (si, gi, ui, sj, gj, uj, sk, gk, uk, ti, fi, tj, fj, tk, fk, xi)
    = si.Proc1            (si,gi,ui,sj,gj,uj,sk,gk,uk,ti,fi,tj,fj,tk,fk,xi)
    ;

agent Proc1 (si, gi, ui, sj, gj, uj, sk, gk, uk, ti, fi, tj, fj, tk, fk, xi)
    = gj.(
        'tj.Proc2          (si,gi,ui,sj,gj,uj,sk,gk,uk,ti,fi,tj,fj,tk,fk,xi)
      + 'fj.xi.uk.ui.Proc  (si,gi,ui,sj,gj,uj,sk,gk,uk,ti,fi,tj,fj,tk,fk,xi)
      )
    ;

agent Proc2 (si, gi, ui, sj, gj, uj, sk, gk, uk, ti, fi, tj, fj, tk, fk, xi)
    = gk.(
        'fk.Proc1          (si,gi,ui,sj,gj,uj,sk,gk,uk,ti,fi,tj,fj,tk,fk,xi)
      + 'tk.ui.Proc3       (si,gi,ui,sj,gj,uj,sk,gk,uk,ti,fi,tj,fj,tk,fk,xi)
      )
    ;

agent Proc3 (si, gi, ui, sj, gj, uj, sk, gk, uk, ti, fi, tj, fj, tk, fk, xi)
    = gk.(
        'fk.Proc3          (si,gi,ui,sj,gj,uj,sk,gk,uk,ti,fi,tj,fj,tk,fk,xi)
      + 'tk.si.Proc1       (si,gi,ui,sj,gj,uj,sk,gk,uk,ti,fi,tj,fj,tk,fk,xi)
      )
    ;

* -- Instantiation --------------------------------------------------------

agent P0 = Proc (s0,g0,u0,s1,g1,u1,sk,gk,uk,t0,f0,t1,f1,tk,fk,x0);
agent P1 = Proc (s1,g1,u1,s0,g0,u0,uk,gk,sk,t1,f1,t0,f0,fk,tk,x1);
agent B0 = Bool (t0,f0,g0,s0,u0);
agent B1 = Bool (t1,f1,g1,s1,u1);
agent Bk = Bool (tk,fk,gk,sk,uk)
		 + Bool (fk,tk,gk,uk,sk);

* -- Actual system --------------------------------------------------------

set S = {   s0, g0, u0, t0, f0,
            s1, g1, u1, t1, f1,
            sk, gk, uk, tk, fk
        };

agent System = ( P0 | P1 | B0 | B1 | Bk ) \ S;

